#pragma kernel CalculateAmplitudes

RWTexture2D<float2> Dx_Dz;
RWTexture2D<float2> Dy_Dxz;
RWTexture2D<float2> Dyx_Dyz;
RWTexture2D<float2> Dxx_Dzz;

Texture2D<float4> H0;
// wave vector x, 1 / magnitude, wave vector z, frequency
Texture2D<float4> WavesData;
float Time;


float2 ComplexMult(float2 a, float2 b)
{
	return float2(a.x * b.x - a.y * b.y, a.x * b.y + a.y * b.x);
}

[numthreads(8,8,1)]
void CalculateAmplitudes(uint3 id : SV_DispatchThreadID)
{
	// [ kx , 1 / length(k) , kz , omega(k) ]
	float4 wave = WavesData[id.xy];
	float phase = wave.w * Time;
	float2 exponent = float2(cos(phase), sin(phase));
	// h(k,t) = H0(k) * exp(i * omega(k) * t) + H0*(-k) * exp(-i * omega(k) * t)
	// H0[id.xy] = [ H0(k) , H0*(-k) ]
	float2 h = ComplexMult(H0[id.xy].xy, exponent)
		+ ComplexMult(H0[id.xy].zw, float2(exponent.x, -exponent.y));
	float2 ih = float2(-h.y, h.x);
	
	// wave.x = kx, wave.y = 1 / magnitude, wave.z = kz
	// Here is the displacement 
	float2 displacementX = ih * wave.x * wave.y;
	float2 displacementY = h;
	float2 displacementZ = ih * wave.z * wave.y;
		 
	// here is the displacement derivative for normal and foam calculation
	float2 displacementX_dx = -h * wave.x * wave.x * wave.y;  
	float2 displacementY_dx = ih * wave.x;
	float2 displacementZ_dx = -h * wave.x * wave.z * wave.y;
		 
	float2 displacementY_dz = ih * wave.z;
	float2 displacementZ_dz = -h * wave.z * wave.z * wave.y;
	
	// X + iZ
	Dx_Dz[id.xy] = float2(displacementX.x - displacementZ.y, displacementX.y + displacementZ.x);
	// Y + idxZ
	Dy_Dxz[id.xy] = float2(displacementY.x - displacementZ_dx.y, displacementY.y + displacementZ_dx.x);
	//dxY + idzY
	Dyx_Dyz[id.xy] = float2(displacementY_dx.x - displacementY_dz.y, displacementY_dx.y + displacementY_dz.x);
	//dxX + idzZ
	Dxx_Dzz[id.xy] = float2(displacementX_dx.x - displacementZ_dz.y, displacementX_dx.y + displacementZ_dz.x);
}

